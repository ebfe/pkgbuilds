# Maintainer: Michael Gehring <mg@ebfe.org>

pkgname=wld-git
pkgver=20140131.6b163d2
pkgrel=1
pkgdesc="A primitive drawing library targeted at Wayland"
arch=('i686' 'x86_64')
url="https://github.com/michaelforney/wld"
makedepends=('git')
depends=('fontconfig' 'libdrm' 'pixman' 'wayland')
provides=('wld')
options=('staticlibs')
source=("$pkgname"::'git+https://github.com/michaelforney/wld')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git log -1 --format="%cd.%h" --date=short | sed 's|-||g'
}

build() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install
}


