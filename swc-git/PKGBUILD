# Maintainer: Michael Gehring <mg@ebfe.org>

pkgname=swc-git
pkgver=20140204.b8123be
pkgrel=1
pkgdesc="A primitive drawing library targeted at Wayland"
arch=('i686' 'x86_64')
url="https://github.com/michaelforney/swc"
makedepends=('git')
depends=('systemd' 'fontconfig' 'libdrm' 'libevdev' 'xcb-util-wm' 'libxkbcommon' 'pixman' 'wld')
provides=('swc')
options=('staticlibs')
source=("$pkgname"::'git+https://github.com/michaelforney/swc')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git log -1 --format="%cd.%h" --date=short | sed 's|-||g'
}

build() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install
}


