# Maintainer: Michael Gehring <mg@ebfe.org>

pkgname=mupdf-cs
_pkgname=mupdf
pkgver=1.3
pkgrel=1
pkgdesc='A lightweight PDF and XPS viewer'
arch=('i686' 'x86_64')
url='http://mupdf.com'
conflicts=('mupdf')
options=('staticlibs')
depends=('openssl' 'libxext')
source=(https://mupdf.googlecode.com/files/$_pkgname-$pkgver-source.tar.gz 0001-continuous-scrolling.patch mupdf-1.3-stack-buffer-overflow-in-xps_parse_color.patch)

sha256sums=('aba8b31bee9cc0a16abedab5e31c81c65996cba5591e62a50a79bea2a63d4478'
            '7722fda313c8106bfa49380bbe665471adcbfd05446bb379c9ec044c7b715170'
            '84160d384832192c68e7ddc75c6e9b512f4b58b22fc06e85e5756ec20feecd47')

prepare() {
	cd $_pkgname-$pkgver-source
	patch -p1 < "${srcdir}"/0001-continuous-scrolling.patch
	patch -p1 < "${srcdir}"/mupdf-1.3-stack-buffer-overflow-in-xps_parse_color.patch
}

build() {
	cd $_pkgname-$pkgver-source
	make build=release prefix=/usr
}

package() {
	cd $_pkgname-$pkgver-source
	make build=release prefix=/usr DESTDIR="$pkgdir" install
	find "$pkgdir"/usr/lib -type f | xargs chmod -v 0644
	ln -s '/usr/bin/mupdf-x11-curl' "$pkgdir"/usr/bin/mupdf
}

